#!/bin/csh -f

set icons = ( airplane.png  brightness.png sync.png         \
              bluetooth.png gps.png phone.png ringer.png radio.png \
              screenon.png wifi.png wifiap.png )

foreach icon ( $icons )
    set name = $icon:r
    set masked = ${name}-m.png
    set final = ${name}-f.png
    set disabled = ${name}_off.png
    set hdpi = ../res/drawable-hdpi/$icon
    set mdpi = ../res/drawable-mdpi/$icon
    set ldpi = ../res/drawable-ldpi/$icon
    echo $name

    convert gradient.png  \
            ${icon} -compose CopyOpacity -composite \
            ${masked}
    convert ${masked} -fx A +matte -blur 1x1 -shade 120x65 -normalize \
            ${masked} -compose Overlay -composite \
            ${masked} -matte -compose Dst_In -composite \
            ${final}
            
    cp $final $hdpi
    convert $final -resize 66.6667% $mdpi
    convert $final -resize 50% $ldpi

    if ( "radio" == $name ) then
        convert ${final} -modulate 50,30 ${disabled}
        cp $disabled ${hdpi:r}_off.png
        convert $disabled -resize 66.6667% ${mdpi:r}_off.png
        convert $disabled -resize 50% ${ldpi:r}_off.png
        rm $disabled
    endif
    
    rm ${masked} ${final}
end


set leds = ( blue.png green.png orange.png red.png grey.png icon.png )

foreach led ($leds)
    set name = $led:r
    set hdpi = ../res/drawable-hdpi/$led
    set mdpi = ../res/drawable-mdpi/$led
    set ldpi = ../res/drawable-ldpi/$led
    echo $name
    
    cp $led $hdpi
    convert $led -resize 66.6667% $mdpi
    convert $led -resize 50% $ldpi
end


